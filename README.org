#+TITLE: My Emacs configuration
#+DATE: 2021-06-04
#+STARTUP: show3levels
#+LANGUAGE: en


* Background

In the early 1990s I was introduced to Emacs. Since then, I tend to bounce between Emacs and other editors (VS Code, Sublime, ...). But currently I am on an Emacs-streak of 5+ years.

When I use Emacs I tend to grow my config until it feels too big. And then I start over. So, here it is. I’ll be working with this for a while.

** Considerations

I use Emacs mostly professionally, mostly for coding (JS, Python, C/C++) projects (org-mode).

With this configuration, I try to stick to the following guidelines:
1. stick to vanilla Emacs as much as possible
2. only add a package when it actually adds value
3. resist the urge to tinker

Ad 1: This means I try to learn vanilla key binding, so no UAC for example. Also, I try to work with built-in functionality, before I install a package (less packages, means less bugs and conflicts). Also, I use a built-in theme.

Ad 2: This means I stay away from eye-candy, unless it improves usability. Also, I do not add a package just because it is clever of hip.

Ad 3: And failing miserably. Because what would be the fun in that? Moreover, I think it is important to [[https://www.bridgeheadsoftware.com/2016/04/training-matters-are-you-too-busy-chopping-wood-to-sharpen-the-axe/][sharpen your axe]].  [[https://www.reddit.com/user/publicvoit/][publicvoit]] nails it by:

#+BEGIN_QUOTE
Everybody is procrastinating. Anybody who disagrees is either lying or she/he is not aware of doing X as procrastinating.
The good news here is that you seem to optimize your working environment while procrastinating.
#+END_QUOTE


Reminder: Start =emacs --debug-init= to debug initialization, =emacs -Q= to skip initialization, =emacs -q --load <file>= to use an alternative init file.

*** Literate configuration

[[http://cleancoder.com/products][Uncle Bob]] [[https://www.goodreads.com/quotes/835238-indeed-the-ratio-of-time-spent-reading-versus-writing-is][teaches]] we spend far more time reading source code than writing it. Hence, it makes sense to optimize for the former, but in reality it seems we don't. Coders are cranking out code as fast as they can type, without regard to readability for others (and their future self). I know I am prone to doing just that.

Instead we could endeavor to make code as readable as possible, and literate programming can help with that. In this specific case, embedding my configuration in an org file allows me to remind me of the effects of certain settings and record the choices that lead up to them.

On the other hand, comments are overrated. If an identifier is sufficiently explained by its name, I do not need a comment. A term that can be found in the manual or can easily be Googled does not need to be explained. To facilitate this and avoid unnecessary searching, I include links to documentation whenever possible.

Some other examples of literate configurations:
- [[https://github.com/novoid/dot-emacs/blob/master/config.org][Karl Voit]]
- [[https://github.com/mwfogleman/.emacs.d][Tasshin Fogleman]]
- [[https://github.com/geolessel/dotfiles/tree/master/emacs/emacs.d][Geoffrey Lessel]]
- [[https://github.com/hrs/dotfiles/blob/main/emacs/dot-emacs.d/configuration.org][Harry R. Schwartz]]
- [[https://github.com/clintonboys/emacs.d/blob/master/init.org][Clinton Boys]]

*** Essentials

I would not want to do without:
1. [[Bootstrap][use-package]]
2. [[Org-mode]]
3. [[Language server protocol][lsp-mode]]
4. [[Version control][Magit]]

*** Limitations

At this point I have decided to keep a number of applications separate from Emacs:
- browse in Chrome
- email in GMail (in Chrome)
- shell (=zsh=) in a terminal

*** Dilemmas

Do I place [[Key bindings][key bindings]] together in a central location, or do I put them with other related settings? The former gives a good overview of all key bindings, while the latter groups related settings. Either is fine (or equally uncomfortable) with me, as long as it is applied consistently.

The same goes for [[Customization][customizations]].

In both cases I choose a central location.

** History

- 2021 :: Moved to literate config file (embedded in org-file)

- 2020 :: Back with a fresh start.
- 2019 :: Trying to go back to (mostly) vanilla Emacs, with minimal config.
- 2018 :: Back to a custom config, using [[https://github.com/jwiegley/use-package][use-package]].
- 2017 :: I decided to give Prelude a go. This is the contents of my ~personal~ directory.

  After some initial misgivings (mainly due to having to relearn Prelude key strokes vs custom ones) I find that my Prelude customization is much smaller than my vanilla Emacs one.

** Convention

Names of functions and variables that I added are preceded by "/steets//".

This file is called =README.org= so github will show it automatically.

* Where are we?

I run Emacs on different systems and platforms. And I like this config to work out of the box on all of them. So:
- I sync my org files through Dropbox
- we need to set some platform-dependent paths

#+BEGIN_SRC emacs-lisp
  (setq steets/org-dir (cond ((eq system-type 'gnu/linux) "~/Dropbox/org/")
                             ((eq system-type 'windows-nt) "S:/org/" "")
                             ((eq system-type 'cygwin) "/cygdrive/s/org/" "")))

  (defun steets/org-path (path)
    "Return a path to an org file"
    (concat steets/org-dir path))
#+END_SRC

* Customization

I prefer to use the customize interface whenever possible, because it gives useful hints and documentation. I store customizations in a separate [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Saving-Customizations.html][file]]:

#+BEGIN_SRC emacs-lisp
  (setq custom-file "~/.emacs.d/custom.el")
  (if (file-exists-p custom-file) (load custom-file))
#+END_SRC

I am aware that this means that I can't keep customizations with their packages. It's just one of those [[Dilemmas][dilemmas]].

The main disadvantage of this choice is that comments in the =custom-set-variables= statement are lost when =customize= updates it. But documentation for each variable can be accessed through =C-h v=.

* Packages
** Bootstrap

I use [[https://github.com/jwiegley/use-package][use-package]] package configuration with [[https://github.com/raxod502/straight.el][straight]] package manager.

The code to bootstrap straight is not here, but in [[file:init.el][init.el]], because of this [[https://github.com/raxod502/straight.el#the-wrong-version-of-my-package-was-loaded][issue]]. TLDR: When emacs loads org-babel to tangle this file, it also loads the built-in org itself. So, by the time use-package gets to configuring org, straight figures (correctly) that org is already loaded and sticks with the currently loaded version, which keeps it from updating it. To work around this, I bootstrap straight and load org in init.el.

Straight does not automatically update packages. I like this, because it make Emacs start up faster. To update manually, run =(straight-pull-all)=.

Replace =use-package= macro with =straight-use-package=:

#+BEGIN_SRC emacs-lisp
  (straight-use-package 'use-package)
  (setq straight-use-package-by-default t)
#+END_SRC

Use-package tips:
- [[https://github.com/jwiegley/use-package#getting-started][:init vs :config]]:
  - Use the =:init= keyword to execute code /before/ a package is loaded.
  - =:config= can be used to execute code /after/ a package is loaded.
- The [[https://github.com/jwiegley/use-package#hooks][:hook]] construct refers to modes, while =add-hook= refers to hooks:
  - =:hook (prog-*mode* . ace-jump-mode)=, vs
  - =(add-hook 'prog-mode-*hook* #'ace-jump-mode)=

** Usability

Get some pretty [[https://github.com/domtronn/all-the-icons.el][icons]] that are needed by several other packages:

#+BEGIN_SRC emacs-lisp
  (use-package all-the-icons)
#+END_SRC

[[https://company-mode.github.io/][COMPlete ANYthing]]

#+BEGIN_SRC emacs-lisp
  (use-package company
    :config (global-company-mode))
#+END_SRC

Add a [[https://github.com/seagle0128/doom-modeline][pretty mode line]]:

#+BEGIN_SRC emacs-lisp
  (use-package doom-modeline
    :hook (after-init . doom-modeline-mode)
    :config
    (setq doom-modeline-unicode-fallback t))
#+END_SRC

Keep the mode line clean by hiding minor-modes with [[https://github.com/emacsmirror/diminish][diminish]] (used by [[Bootstrap][use-package]]):

#+BEGIN_SRC emacs-lisp
  (use-package diminish)
#+END_SRC

Help to learn key bindings by [[https://github.com/justbur/emacs-which-key][suggesting keys]]:

#+BEGIN_SRC emacs-lisp
  (use-package which-key
    :diminish which-key-mode
    :config
    (which-key-mode))
#+END_SRC

[[https://github.com/creichert/ido-vertical-mode.el][ido-vertical-mode]] to make [[https://www.gnu.org/software/emacs/manual/html_mono/ido.html][IDO]] more to my taste:

#+BEGIN_SRC emacs-lisp
  (use-package ido-vertical-mode
    :config
    (ido-mode 1)
    (ido-vertical-mode 1)
    (setq ido-vertical-define-keys 'C-n-C-p-up-down-left-right))
#+END_SRC

I like my current line to be visible, including syntax highlighting:

#+BEGIN_SRC emacs-lisp
  (set-face-attribute 'hl-line nil :inherit nil :background "grey25")
#+END_SRC

** Development

*** Language server protocol

Use [[https://github.com/emacs-lsp/lsp-mode][lsp-mode]] for support of programming languages ([[https://langserver.org/][LSP]]).

The line below is needed to get LSP right:

#+BEGIN_SRC emacs-lisp
  (straight-pull-package 'melpa)
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (use-package lsp-mode
    :init (setq lsp-keymap-prefix "C-c h") ;; alternatives - "C-l", "C-c l"
    :hook ((python-mode . lsp)
           (bash-mode . lsp)
           (dockerfile-mode . lsp)
           (c-mode . lsp)
           (c++-mode . lsp)
           (js-mode . lsp)
           (json-mode . lsp)
           (typescript-mode . lsp)
           (lsp-mode . lsp-enable-which-key-integration))
    :commands lsp)
#+END_SRC

Manage [[https://emacs-lsp.github.io/lsp-mode/tutorials/how-to-turn-off/][UI elements]] of lsp-mode:

#+BEGIN_SRC emacs-lisp
  (use-package lsp-ui
    :commands lsp-ui-mode
    :config
    (setq lsp-ui-doc-position 'at-point
          lsp-ui-doc-show-with-cursor nil
          lsp-lens-enable nil
          lsp-ui-flycheck-enable t))
#+END_SRC

LSP needs some help for Python:

#+BEGIN_SRC emacs-lisp
  (use-package lsp-python-ms
    :init (setq lsp-python-ms-auto-install-server t)
    :hook (python-mode . (lambda ()
                           (require 'lsp-python-ms)
                           (lsp)))
    :config
    (setq lsp-python-ms-executable
          "~/.local/share/virtualenvs/.emacs.d-Qr0izAws/bin/pylsp"))
#+END_SRC

*** Version control

[[https://magit.vc/][Magit]]

#+BEGIN_SRC emacs-lisp
  (use-package magit
    :bind ("C-x g" . magit-status))
#+END_SRC

[[https://github.com/emacsorphanage/git-gutter][git-gutter]] to show changes in the fringe (could not get alternative [[https://github.com/dgutov/diff-hl][diff-hl]] to work):

#+BEGIN_SRC emacs-lisp
  (use-package git-gutter
    :if (display-graphic-p) ; no fringe in terminal mode
    :config (global-git-gutter-mode))
#+END_SRC

*** Docker

I always have several projects running at the same time. Often, these projects use very different development chains and deployment environments. [[https://docs.docker.com/][Docker]] makes this work flow a breeze and can be [[https://github.com/Silex/docker.el][managed]] from Emacs.

#+BEGIN_SRC emacs-lisp
  (use-package docker)
#+END_SRC

Start with =C-c l D= (capital).

See also the [[Language modes]] below.

*** Language modes

Add modes that Emacs doesn't support out of the box.

#+BEGIN_SRC emacs-lisp
  (use-package gherkin-mode)

  (use-package jinja2-mode
    :mode ("\\.mustache$" "\\.djhtml$" "\\.jinja2$" ))

  (use-package markdown-mode
    :mode (("\\.markdown\\'" . markdown-mode)
           ("\\.md\\'" . markdown-mode)))

  (use-package docker-compose-mode)
  (use-package dockerfile-mode
    :mode "^Dockerfile$")
#+END_SRC

*** Formatting

[[https://github.com/lassik/emacs-format-all-the-code][format-all]] for standard formatting when saving source code. See also =astylerc=.

#+BEGIN_SRC emacs-lisp
  (use-package format-all
    :hook
    ((prog-mode . format-all-mode)
     (format-all-mode . format-all-ensure-formatter)))
#+END_SRC

** Text processing

On the fly [[http://www-sop.inria.fr/members/Manuel.Serrano/flyspell/flyspell.html][spell checking]] for text modes and code comments:

#+BEGIN_SRC emacs-lisp
  (use-package flyspell
    :init
    (setq ispell-program-name "aspell"
          ispell-list-command "--list")
    :hook
    ((prog-mode . flyspell-prog-mode)
     (text-mode . flyspell-mode))
    :config
    ;; fix mouse
    (define-key flyspell-mouse-map [down-mouse-3] #'flyspell-correct-word)
    (define-key flyspell-mouse-map [mouse-3] #'undefined))
#+END_SRC

[[https://github.com/bnbeckwith/writegood-mode][Writegood]] to find common writing problems:

#+BEGIN_SRC emacs-lisp
  (use-package writegood-mode
    :commands writegood-mode
    :diminish writegood-mode
    :hook text-mode)
#+END_SRC

*** Org-mode

#+BEGIN_SRC emacs-lisp
  (use-package org
    :hook
    ((org-mode . org-indent-mode)
     (org-mode .
               (lambda ()
                 ;; file modification date
                 (set (make-local-variable 'time-stamp-format) "%:y-%02m-%02d")
                 (set (make-local-variable 'time-stamp-start) "^#\\+DATE: +")
                 (set (make-local-variable 'time-stamp-end) "$")

                 ;; Switch language for Org file, if a `#+LANGUAGE:' meta-tag is on top 14 lines.
                 (save-excursion
                   (goto-line 15)
                   (if (re-search-backward "#\\+LANGUAGE: +\\([A-Za-z_]*\\)" 1 t)
                       (ispell-change-dictionary (match-string 1))))
                 )))
    :config
    (org-clock-persistence-insinuate)

    (setq org-agenda-custom-commands
          '(("h" "TODOs history of closed tasks"
             todo "DONE|CANCELLED|DEFERRED"
             ((org-agenda-log-mode-items '(clock state))
              (org-agenda-sorting-strategy '(timestamp-down))))
            ("o" "Open TODOs"
             todo "TODO|NEXT|ACTIVE"
             ((org-agenda-log-mode-items '(clock state))
              (org-agenda-sorting-strategy '(priority-down todo-state-down timestamp-down))))
            ("p" "Review previous month"
             agenda ""
             ((org-agenda-span 'month)
              (org-agenda-start-day "-1m")
              (org-agenda-start-with-log-mode t)
              (org-agenda-log-mode-items '(clock state))
              (org-agenda-archives-mode t) ; include archive files
              ))
            ("l" "TODOs for later"
             todo "SOMEDAY"
             ((org-agenda-sorting-strategy '(todo-state-up priority-down)))))
          org-capture-templates
          `(("b" "blog" entry (file "~/src/dwim/posts/index.org") "* DRAFT %?\n%a"
             :prepend t
             :emtpy-lines 1
             :unnarrowed t)
            ("h" "habit" entry (file org-default-notes-file) "* NEXT %?\n%U\n%a
        SCHEDULED: %(format-time-string \"%<<%Y-%m-%d %a .+1d/3d>>\")
        :PROPERTIES:
        :STYLE: habit
        :REPEAT_TO_STATE: NEXT
        :END:
        ")
            ("j" "journal" entry (file+olp+datetree org-default-notes-file "Journal") "")
            ("z" "zettelkasten" entry (file+olp+datetree org-default-notes-file "Zettelkasten") "")
            ("m" "meeting" entry (file org-default-notes-file) "* %U: %?               :meeting:\nWith:\n\n")
            ("n" "note" entry (file+headline org-default-notes-file "Personal Knowledge Base") "* %?\n%U\n%a"
             :prepend t
             :emtpy-lines 1
             :unnarrowed t)
            ("t" "todo" entry (file+headline org-default-notes-file "Work") "* TODO %?\n%U\n%a"
             :prepend t
             :emtpy-lines 1
             :unnarrowed t)
            ("w" "writing" entry (file "~/src/fiction/process.org") "* %?\n%a"
             :prepend t
             :emtpy-lines 1
             :unnarrowed t)
            ))

    ;; not sure how to achieve this with :bind(-keymap)
    (define-prefix-command 'steets/org-map)
    (define-key mode-specific-map "o" 'steets/org-map)
    (define-prefix-command 'steets/zettel-map)
    (define-key mode-specific-map "z" 'steets/zettel-map)

    :bind (:map steets/org-map
                ("a" . org-agenda)
                ("c" . org-capture)
                ("d" . deft)
                ("e" . org-entities-help)
                ("f" . (lambda ()
                         (interactive)
                         (steets/find-file org-directory)))
                ("i" . org-insert-link)
                ("j" . org-clock-goto)
                ("l" . org-store-link)
                ("n" . (lambda ()
                         (interactive)
                         (find-file (steets/org-path "notes.org"))))
                :map steets/zettel-map
                ("/" . deft)
                ("b" . org-roam-switch-to-buffer)
                ("c" . org-roam-capture)
                ("d" . org-roam-find-directory)
                ("f" . org-roam-find-file)
                ("h" . org-roam-find-index)
                ("i" . org-roam-insert)
                ("t" . org-roam-buffer-toggle-display)
                ))
#+END_SRC

Use [[https://www.emacswiki.org/emacs/AutoInsertMode][autoinsert]] to start a new org file with some basic content:

#+BEGIN_SRC emacs-lisp
  (use-package autoinsert
    :hook (find-file . auto-insert)
    :config
    (setq auto-insert-query nil)
    (auto-insert-mode 1)
    (setq auto-insert-alist nil) ;; remove this to restore defaults
    (define-auto-insert '(org-mode . "org-mode file")
      '("org-mode header"
        "#+TITLE: " (file-name-base (buffer-file-name)) \n
        "#+DATE: " (format-time-string "%Y-%m-%d") \n
        "#+LANGUAGE: nl" \n
        \n
        )))
#+END_SRC

[[https://www.orgroam.com/manual.html][Zettelkasten]] in Org:

#+BEGIN_SRC emacs-lisp
  (use-package org-roam
    :after org
    :config
    (setq org-roam-db-update-method 'immediate)
    :hook
    (org-mode . org-roam-mode)  ;; could also be from after-init-hook
    :custom
    (org-roam-directory (steets/org-path "roam"))
    (org-roam-capture-templates
     '(("d" "default" plain (function org-roam--capture-get-point)
        "%?"
        :file-name "%<%Y-%m-%d-%H%M%S>-${slug}"
        :head "#+ROAM_TAGS: \n#+ROAM_ALIAS: \n"
        :unnarrowed t)))
    )
#+END_SRC

Use [[https://www.orgroam.com/manual.html#Full_002dtext-search-interface-with-Deft][deft]] to search Zettels:

#+BEGIN_SRC emacs-lisp
  (use-package deft
    :after org-roam
    :config (setq deft-directory org-roam-directory
                  deft-extensions '("md" "org")
                  deft-default-extension "org"))
#+END_SRC

* Functions

#+BEGIN_SRC emacs-lisp
  (require 'cl-lib)


  (defun steets/read-file (filePath)
    "Return FILEPATH's file content."
    (with-temp-buffer
      (insert-file-contents filePath)
      (buffer-string)))


  (defun steets/find-regexp (regexp)
    "Return first occurrence of REGEXP in current buffer."
    (goto-char (point-min))
    (search-forward-regexp regexp)
    (match-string 1))


  (defun steets/word-frequency ()
    "Return a hash with word frequencies from current buffer."
    (interactive)
    (let ((table (make-hash-table :test 'equal :size 128)))
      (save-excursion
        (goto-char (point-min))
        (while (re-search-forward "\\<[[:word:]]+\\>" nil t)
          (let ((word (downcase (match-string 0))))
            (message word)
            (puthash word (1+ (gethash word table 0)) table))
          ))
      (maphash (lambda (key val)
                 (message (format "%s: %d" key val)))
               table)))


  (defun steets/path-join (root &rest dirs)
    "like Python's os.path.join"
    (if (not dirs) root
      (apply 'steets/path-join
             (expand-file-name (car dirs) root)
             (cdr dirs))))


  (defun steets/find-file(directory)
    "Find a file in DIRECTORY"
    (interactive)
    (find-file (read-file-name "Find file: " directory)))


  (defun steets/god-mode-line ()
    "Change mode line in God mode "
    (cond (god-local-mode
           (progn
             (set-face-background 'mode-line "tomato")
             (set-face-background 'mode-line-inactive "firebrick")))
          (t (progn
               (set-face-background 'mode-line steets/mode-line-background)
               (set-face-background 'mode-line-inactive steets/mode-line-background-inactive)))))


  (defun steets/org-show-just-me (&rest _)
    "Fold all other trees, then show entire current subtree."
    (interactive)
    (org-overview)
    (org-reveal)
    (org-show-subtree))


  (defun steets/switch-to-minibuffer ()
    "switch to minibuffer window (if active)"
    (interactive)
    (when (active-minibuffer-window)
      (select-frame-set-input-focus (window-frame (active-minibuffer-window)))
      (select-window (active-minibuffer-window))))
#+END_SRC

* Key bindings

I like to keep all my key bindings in one place. This means I cannot keep them with related configuration, but that's just one of the [[Dilemmas][dilemmas]].

[[https://github.com/noctuid/general.el][General]] seems an attractive option, but I could not figure out how to convert my keybindings.

** User keys

The accepted convention is to use =C-c <letter>= (=mode-specific-map=) for user key bindings.

[[https://www.gnu.org/software/emacs/manual/html_node/elisp/Keymaps.html][Keymaps]] provide an elegant way of grouping related key bindings. I use:
- =C-c o= for org (see [[Org-mode]])
- =C-c z= for org zettelkasten (see [[Org-mode]])
- [[Helpful]] overrides =C-h= help keys.

I used to have a keymap to visit files I open often, but realized this is duplicating [[https://www.gnu.org/software/emacs/manual/html_node/emacs/Bookmarks.html][bookmarks]]:
Keymap =C-x r= with =m= to make a bookmark, =b= to open it, and =l= to list them.

*** Toggle

Toggle modes, inspired by [[http://endlessparentheses.com/the-toggle-map-and-wizardry.html][endless]]: "The manual recommends =C-c= for user keys, but =C-x t= is always free, whereas =C-c t= is used by some modes."

#+BEGIN_SRC emacs-lisp
  (define-prefix-command 'steets/toggle-map)
  (define-key mode-specific-map "t" 'steets/toggle-map)

  (define-key steets/toggle-map "c" 'writeroom-mode)  ; clean
  (define-key steets/toggle-map "e" 'toggle-debug-on-error)
  (define-key steets/toggle-map "f" 'format-all-buffer)
  (define-key steets/toggle-map "g" 'git-timemachine-toggle)
  (define-key steets/toggle-map "h" 'hs-toggle-hiding)
  (define-key steets/toggle-map "H" 'hs-hide-all)
  (define-key steets/toggle-map "m" 'steets/switch-to-minibuffer)
  (define-key steets/toggle-map "r" 'dired-toggle-read-only)
  (define-key steets/toggle-map "s" 'flyspell-mode)
  (define-key steets/toggle-map "S" 'hs-show-all)
  (define-key steets/toggle-map "t" 'toggle-truncate-lines)
  (define-key steets/toggle-map "w" 'whitespace-mode)
#+END_SRC

*** Launch

Launch applications, inspired by [[http://endlessparentheses.com/launcher-keymap-for-standalone-features.html][endless]].

#+BEGIN_SRC emacs-lisp
  (define-prefix-command 'steets/launcher-map)
  (define-key mode-specific-map "l" 'steets/launcher-map)

  (define-key steets/launcher-map "c" 'calc)
  (define-key steets/launcher-map "D" 'docker)
  (define-key steets/launcher-map "d" 'ediff-buffers)
  (define-key steets/launcher-map "f" 'find-dired)
  (define-key steets/launcher-map "g" 'grep)
  (define-key steets/launcher-map "h" 'man)
  (define-key steets/launcher-map "l" 'package-list-packages)
  (define-key steets/launcher-map "m" 'compile)
  (define-key steets/launcher-map "p" 'steets/password)
  (define-key steets/launcher-map "s" 'flyspell-buffer)
  (define-key steets/launcher-map "t" 'ansi-term)
#+END_SRC

*** Others

#+BEGIN_SRC emacs-lisp
  (define-key mode-specific-map "b" 'eval-buffer)
  (define-key mode-specific-map "c" 'customize-variable)
  (define-key mode-specific-map "d"  ; duplicate line
    (lambda () (interactive)
      (save-excursion
        (let ((kill-read-only-ok t) deactivate-mark)
          (read-only-mode 1) (kill-whole-line) (read-only-mode 0) (yank)))))
  ;; (define-key mode-specific-map "h" RESERVED FOR LSP
  (define-key mode-specific-map "j" (lambda () (interactive) (join-line -1)))
  (define-key mode-specific-map "m" 'steets/switch-to-minibuffer)
  (define-key mode-specific-map "s" 'sort-lines)
  (define-key mode-specific-map "S" 'window-swap-states) ;
  (define-key mode-specific-map "u"  ;; dos2unix
    (lambda() (interactive)
      (revert-buffer-with-coding-system 'unix t)
      (save-excursion
        (goto-char (point-min))
        (while (search-forward "" nil t) (replace-match "")))))
  (define-key mode-specific-map "v" 'insert-char)
  (define-key mode-specific-map "w" 'count-words)
#+END_SRC

** Overrides

For consistency with other applications:

#+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "C-<tab>") 'next-buffer)                ; default C-x <right>
  (global-set-key (kbd "C-S-<iso-lefttab>") 'previous-buffer)  ; default C-x <left>
  (global-set-key (kbd "C-+") 'text-scale-increase)
  (global-set-key (kbd "C--") 'text-scale-decrease)
  (global-set-key (kbd "C-/") 'comment-line)                   ; defaul C-x C-;
  (global-set-key (kbd "C-z") 'undo)                           ; default suspend-frame
#+END_SRC

Override default behavior with improved versions:

#+BEGIN_SRC emacs-lisp
  (global-set-key (kbd "M-/") 'hippie-expand)
  (global-set-key (kbd "M-<up>")  ; move line up
                  (lambda () (interactive)
                    (transpose-lines 1) (forward-line -2) (indent-according-to-mode)))
  (global-set-key (kbd "M-<down>")  ; move line down
                  (lambda () (interactive)
                    (forward-line 1) (transpose-lines 1) (forward-line -1)
                    (indent-according-to-mode)))

  (global-set-key (kbd "C-x C-b") 'ibuffer)                    ; default list-buffers
  (global-set-key (kbd "C-x k") 'kill-this-buffer)             ; default kill-buffer
#+END_SRC

* Hooks

General hooks that are not related to packages:

#+BEGIN_SRC emacs-lisp
  (add-hook 'before-save-hook 'whitespace-cleanup)
  (add-hook 'text-mode-hook 'visual-line-mode)
  (add-hook 'prog-mode-hook 'display-line-numbers-mode)
  (add-hook 'prog-mode-hook 'hs-minor-mode)
  (add-hook 'prog-mode-hook 'outline-minor-mode)
  (add-hook 'after-save-hook
            'executable-make-buffer-file-executable-if-script-p)
#+END_SRC

* Final tweaks

#+BEGIN_SRC emacs-lisp
  (display-time)

  (set-language-environment "UTF-8")

  (fset 'yes-or-no-p 'y-or-n-p)

  (put 'narrow-to-region 'disabled nil)

  (require 'iso-transl) ; needed since upgrade to Ubuntu 21.04

  (setq gc-cons-threshold 100000000) ; Make things faster, particularly in lsp-mode.

  ;; start in notes
  (find-file (steets/org-path "notes.org"))
#+END_SRC

* Playground

Space to try out new stuff.

Tip: insert new code block with =C-c C-,=

** Password generator

A replica of https://otech.nl/pwd/:

#+begin_src emacs-lisp
      (defun steets/decode-hex-string (hex-string)
        "Convert HEX string to ASCII string
      as per https://stackoverflow.com/questions/12003231/how-do-i-convert-a-string-of-hex-into-ascii-using-elisp"
        (let ((res nil))
          (dotimes (i (/ (length hex-string) 2) (apply #'concat (reverse res)))
            (let ((hex-byte (substring hex-string (* 2 i) (* 2 (+ i 1)))))
              (push (format "%c" (string-to-number hex-byte 16)) res)))))

      (defun steets/b64_md5 (s)
        "Returns an base64 encoude MD5 hash of s (Replica of http://pajhome.org.uk/crypt/md5/)"
        (base64-encode-string
         (steets/decode-hex-string (secure-hash 'md5 (downcase s)))))

      (defun steets/b64_md5_slice (s len)
          (substring (steets/b64_md5 s) 0 len))

      (defun steets/password ()
        "Get hash for user values (replica of https://otech.nl/pwd/) and put in kill ring"
        (interactive)
        (let ((all "") (val "dymmy"))
          (while (not (string= val ""))
            (setq val (read-passwd "Value (RET to end): " nil ""))
            (setq all (concat all val)))
          (kill-new (steets/b64_md5_slice all 16))
          (message "Result is in kill-ring")))
#+end_src



** Writeroom

Distraction-free [[https://github.com/joostkremers/writeroom-mode][writing environment]] (alternative: [[https://github.com/rnkn/olivetti][olivetti]]):

#+begin_src emacs-lisp
  (use-package writeroom-mode)
#+end_src

** Helpful

Improve Emacs' already impressive [[https://github.com/Wilfred/helpful][help]]-system:

#+begin_src emacs-lisp
  (use-package helpful
    :bind
    (("C-h f" . helpful-callable)
     ("C-h v" . helpful-variable)
     ("C-h k" . helpful-key)
     ("C-c C-d" . helpful-at-point)
     ("C-h F" . helpful-function)
     ("C-h C" . helpful-command)))
#+end_src

#+RESULTS:
: helpful-command


** Wishlist
:PROPERTIES:
:header-args: :tangle no
:END:

Packages to be started manually.

[[https://github.com/realgud/realgud][Debugging]]

#+begin_src emacs-lisp
  (use-package realgud
    :commands (realgud:gdb
               realgud:pdb))
#+end_src


[[https://github.com/emacs-tw/awesome-emacs][Stuff to try]]:

- https://github.com/jwiegley/emacs-async
- https://github.com/Malabarba/elisp-bug-hunter
- https://github.com/magnars/multiple-cursors.el
- https://github.com/realgud
- https://gitlab.com/jabranham/system-packages provides basic access to system package manager, but no gui
- https://github.com/pashky/restclient.el
- https://github.com/zweifisch/enlive
- https://github.com/noctuid/general.el
- http://wyrick.org/source/elisp/dot-mode/
- https://github.com/adamrt/sane-term or https://www.emacswiki.org/emacs/MultiTerm
- https://www.emacswiki.org/emacs/Icicles
- https://www.emacswiki.org/emacs/BookmarkPlus

** Graveyard
:PROPERTIES:
:header-args: :tangle no
:END:

Rejected configuration (will not be [[https://orgmode.org/manual/Extracting-Source-Code.html][tangled]]):

- Could not get to work:
  - https://github.com/sheijk/hideshowvis
  - [[https://github.com/tarsius/keycast][keycast]] does not [[https://github.com/seagle0128/doom-modeline/issues/122][work]] well with doom-modeline
- minibuffer completion
  - https://github.com/minad/vertico
  - https://github.com/raxod502/selectrum#selectrum-in-comparison-to-other-completion-systems
- https://github.com/atykhonov/google-translate


#+BEGIN_SRC emacs-lisp
  ;; The Uncompromising Python Code Formatter
  ;; https://github.com/psf/black
  (use-package blacken
    :config
    :hook (python-mode . blacken-mode))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (with-eval-after-load 'dired
    (define-key dired-mode-map (kbd "<return>") 'dired-find-alternate-file))
#+END_SRC

#+BEGIN_SRC emacs-lisp
  (use-package apt-utils
    :straight (apt-utils
               :type git
               :host github
               :repo "emacsmirror/emacswiki.org"
               :branch "master"
               :files ("apt-utils.el")))
#+END_SRC

*** Email

[[https://wanderlust.github.io/wl-docs/wl.html][Wanderlust]]
https://archive.casouri.cat/note/2018/wanderlust/index.html
https://enzuru.medium.com/emacs-26-wanderlust-and-modern-gmail-authentication-36e1ae61471f
http://juanjose.garciaripoll.com/blog/emacs-wanderlust-email/index.html

#+begin_src emacs-lisp
  (use-package wl
    ;; The name of the package in MELPA is "wanderlust" but the
    ;; feature provided by that package is 'wl
    :straight `(wanderlust)
    :ensure wanderlust
    :init
    (setq
     elmo-imap4-default-authenticate-type 'clear
     elmo-imap4-default-port '993
     elmo-imap4-default-server "imap.gmail.com"
     elmo-imap4-default-stream-type 'ssl
     elmo-imap4-default-user "rsmetix@gmail.com"
     elmo-imap4-use-modified-utf7 t  ; For non ascii-characters in folder-names

     ;; SMTP
     wl-smtp-authenticate-type "plain"
     wl-smtp-connection-type 'starttls
     wl-smtp-posting-port 587
     wl-smtp-posting-server "smtp.gmail.com"
     wl-smtp-posting-user "rsmetix"

     wl-default-folder "%inbox"
     wl-default-spec "%"
     wl-draft-folder "%[Gmail]/Drafts"
     wl-fcc-force-as-read t  ; Mark sent messages as read
     wl-local-domain "gmail.com"
     wl-message-id-domain "smtp.gmail.com"
     wl-message-ignored-field-list '("^.*")
     wl-message-visible-field-list '("^From:" "^To:" "^Cc:" "^Date:" "^Subject:")
     wl-quicksearch-folder "%[Gmail]/All Mail:rsmetix@imap.gmail.com"
     wl-trash-folder "%[Gmail]/Trash"
     wl-from "René Steetskamp <steets@otech.nl>"
     wl-folder-check-async t

     wl-message-sort-field-list wl-message-visible-field-list
     wl-summary-width 120 ;; No width
     wl-summary-default-sort-spec 'date
     wl-message-window-size '(1 . 2)

     ;; Always download emails without confirmation
     wl-prefetch-threshold nil
     wl-message-buffer-prefetch-threshold nil
     elmo-message-fetch-threshold nil

     ;; Rendering of messages using 'shr', Emacs' simple html
     ;; renderer, but without fancy coloring that distorts the
     ;; looks
     mime-view-text/html-previewer 'shr
     shr-use-colors nil

     wl-draft-config-alist
     '(((string-match "1" "1")
        (bottom . "\n--\n") (bottom-file . "~/.signature"))
       )

     ;; don't ****ing split large messages
     mime-edit-split-message nil
     )

    (autoload 'wl-user-agent-compose "wl-draft" nil t)
    (if (boundp 'mail-user-agent)
        (setq mail-user-agent 'wl-user-agent))
    (if (fboundp 'define-mail-user-agent)
        (define-mail-user-agent
          'wl-user-agent
          'wl-user-agent-compose
          'wl-draft-send
          'wl-draft-kill
          'mail-send-hook))
    )
#+end_src

[[https://github.com/emacsmirror/git-timemachine][Git timemachine]] allows you to browse the history of a file:

#+begin_src emacs-lisp
  (use-package git-timemachine)
#+end_src
